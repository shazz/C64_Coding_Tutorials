!let debug = 1
; ------------------------------------------------------------------------------------------
; Generic Macros
; ------------------------------------------------------------------------------------------
!include "../macros/macros.jasm"

; ------------------------------------------------------------------------------------------
; Specific Macros
; ------------------------------------------------------------------------------------------
; Declare zero-page offset helper
!let zp = {
	xsintab_counter:    $03,
    ysintab_counter:    $04,
    x_positions:        $05,    ; 16 bytes
    y_positions:        $15,    ; nb_rows bytes (9)
    irq_lines:          $1E     ; nb_rows bytes (9)
}

; ------------------------------------------------------------------------------------------
; Plugins
; ------------------------------------------------------------------------------------------
!use "../macros/utils" as utils
!use "../macros/maths" as maths
!use "../macros/sid"   as sid_loader

; ------------------------------------------------------------------------------------------
; Variables
; ------------------------------------------------------------------------------------------
!let desc = loadJson("data/skull5.json")
!!utils.debug(desc)
!let sid = sid_loader("data/Meetro.sid")
!!utils.debug(sid)

!let SPRITE_COLS = 8

!let FIRST_IRQ = 40
!let TOP_POS = 50

; sprite X sine movement
!let SINX_LEN = 60
!let SINX_SCALE = 30
!let SINX_PAD = 80

; sprite Y sine movement
!let SINY_LEN = 40
!let SINY_SCALE = 10
!let SINY_PAD = TOP_POS+SINY_SCALE

!let desc_charset = loadJson("data/charset_50s_5x4_padded.json")
!!utils.debug(desc_charset)
!let CHARSET_WIDTH = desc_charset.charset_width
!let CHARSET_HEIGHT = desc_charset.charset_height
!let CHARSET_SIZE = CHARSET_WIDTH*CHARSET_HEIGHT*8
!let UPDATE_EVERY = 2
; ------------------------------------------------------------------------------------------
; Program
; ------------------------------------------------------------------------------------------
+basic_start(entry)

;--------------------------------------------------------------
; Execution starts here
;--------------------------------------------------------------
entry: { 
    jsr init_sprites                                       ; relocated in another segment else music doesn't fit
    jsr init_charset
    jsr init_system

forever: 
    jmp forever                                             ; at this point, only IRQs do something
}

; ------------------------------------------------------------------------------------------
; IRQs
; ------------------------------------------------------------------------------------------

; this stable IRQ will be repeated / modified for each row of sprite (+1 to clear), then setup the next
sprite_irqs: { 
    !for i in range(desc.nb_rows+1) {
        +double_irq(end, irq_stable)                        ; setting the stable IRQ needs 2 rasterlines

    irq_stable:
        txs                                                 ; we're now at cycle 25 (+/- jitter) after txs
        +waste_cycles(33)                                   ; waste 29 to be at cycle 25+33=58 just before the DMA steal

        ; at this point we have a stable IRQ at cycle 14 after the DMA
        +cpu_time_start()

        ; first irq, reset X pos
        !if (i == 0) {
            ldx zp.ysintab_counter
            lda ysintab,x

            ; compute IRQ rasterlines based on new top Y
            clc
            adc #20-3
            sta zp.irq_lines
            !for row in range(1, desc.nb_rows) {
                ; clc
                adc #20
                sta zp.irq_lines+row
            }

            ; compute row Y positions
            lda ysintab,x
            !for spr in range(desc.nb_rows) {
                sta zp.y_positions+spr
                ; clc
                adc #21
            }
            inx
            cpx #SINY_LEN
            bne no_reset_y
            ldx #0
        no_reset_y:
            stx zp.ysintab_counter

            ; compute 16bits X positions
            ldy #0
            ldx zp.xsintab_counter
            lda xsintab,x  
            !for spr in range(SPRITE_COLS) {
                sta zp.x_positions+(spr*2)
                sty zp.x_positions+(spr*2)+1
            }
            !for spr in range(SPRITE_COLS) {
                +inc16(zp.x_positions+(spr*2), 24*spr)
                lda zp.x_positions+(2*spr)
                sta vic.sprite0_x+(2*spr)	
            }
            inx
            cpx #SINX_LEN
            bne no_reset_x
            ldx #0
        no_reset_x:
            stx zp.xsintab_counter

            ; set high byte sprite x
            lda zp.x_positions+(2*7)+1		; get msb7 [6,5,4,3,2,1,0] 
            !for spr in utils.reverse(range(SPRITE_COLS)) { 	
                asl							; msb << 1
                ora zp.x_positions+(2*spr)+1	; or with msb-1
            }
            sta vic.sprites_x
        }

        ; last row, replace by blank data (here, first one, not that good
        !if (i == desc.nb_rows) {
            lda #(sprite_data/64)
            sta $07f8 + 0
            sta $07f8 + 1
            sta $07f8 + 2
            sta $07f8 + 3
            sta $07f8 + 4
            sta $07f8 + 5
            sta $07f8 + 6
            sta $07f8 + 7

            +cpu_time_stop()

            +irq_end(border_irq, 251, TRUE)              ; last loop turn, go back to top
        
        ; for other rows
        } else {     
            lda zp.y_positions+i
            !for y in range(SPRITE_COLS) {
                sta vic.sprite0_y+(y*2)                 ; adjust next Y positions starting at TOP_POS, +21 pixels,..
            }

            ldx #((sprite_data+($200*i))/64)+4          ; optimized code to set the sprites pointers
            lda #$fb
            sax $07f8 + 0                              
            stx $07f8 + 4   
            inx
            sax $07f8 + 1
            stx $07f8 + 5
            inx
            sax $07f8 + 2
            stx $07f8 + 6
            inx
            sax $07f8 + 3
            stx $07f8 + 7

            +cpu_time_stop()

            +irq_end(end_loop, zp.irq_lines+i, FALSE)
        end_loop:
        }
    end:
    }
}

border_irq: {
    +irq_start(end)

	lda #$00
	sta $d011

    +cpu_time_start()
    jsr sid.play	; call sid player
    +cpu_time_stop()

    jsr time_to_update

    ; reenable borders
	lda #$1b
	sta $d011

	+irq_end(sprite_irqs, FIRST_IRQ, TRUE)
end:
}

!!utils.debug(["end of IRQ code", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $1000: music driver and data
; ------------------------------------------------------------------------------------------
* = sid.startAddress
music:  !byte sid.data
end_music:
!!utils.debug(["end of MUISC code and data", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $2000: displayed charset, set by vic.mem_setup
; ------------------------------------------------------------------------------------------
* = $2000
current_charset: !fill CHARSET_WIDTH, 0
!!utils.debug(["end of CHARSET data", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $2800: as to be visible from selected VIC back (0: $0000 -> $3fff)
; ------------------------------------------------------------------------------------------
* = $2800
sprite_data: !binary desc.datafile
!!utils.debug(["end of SPRITE data", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $4000: Init
; ------------------------------------------------------------------------------------------
* = $4000
init_sprites: {
    
    lda #desc.background_color
    sta vic.bck_col                                 ; set window color to sprite transparent color
    sta vic.border_col                              ; set border color to sprite transparent color

    lda #%00011000                                  ; => character mem at $2000 
    sta vic.mem_setup                               ; => screen mem at $0400

    lda vic.scr_ctrl2                               ; activate multicolor mode
    ora #16                                         ; set bit 4 to 1
    sta vic.scr_ctrl2

    +clear_screen({"clearByte": $20, "screen": $0400, usePtr: FALSE})

	lda #0
    sta vic.sprite_dblx  	                        ; no double width
    sta vic.sprite_dblh			                    ; no double height
	sta vic.sprite_pri	        		            ; no priority

    lda #%11111111                                  ; all sprites multicolor
    sta vic.sprite_colmode                          ; single color sprites

    lda #desc.multi_color1                               ; set sprite first shared color
    sta vic.sprite_extra_col1

    lda #desc.multi_color2                               ; set sprite first shared color
    sta vic.sprite_extra_col2

    lda #desc.sprite_color                               ; set sprite specific color
    !for i in range(SPRITE_COLS) {
        sta vic.sprite0_color+i
    }

    lda #TOP_POS
    !for y in range(SPRITE_COLS) {
        sta vic.sprite0_y+(y*2)                     ; set first y pos
    }    

    lda #(1<<SPRITE_COLS)-1		       
    sta vic.sprite_en		                        ; enable sprites

    rts
}

init_charset: {

    jsr update_charset

    lda charcolors+0
    sta vic.bck_col         ; set window color to charset transparent color
    sta vic.border_col      ; set border color to charset transparent color

    lda charcolors+1        ; set charset first color
    sta vic.back_extra_col1

    lda charcolors+2                    ; set charset 2nd color
    sta vic.back_extra_col2

; copy the character memory tiles bitmap to $0400
; this code is not generic, it implied the tile is 4 lines height
    !for y in range(7) {                    ; copy 25 lines (only copy the first line for line 25)
        ldx #0
    copy_line:  
        !if (y != 6) {
            !for i in range(4) {
                lda screencodes+(i*40),x
                sta $0400+(i*40)+(y*160),x
            }
        } else {                            ; special case for last line
            !for i in range(1) {
                lda screencodes+(i*40),x
                sta $0400+(i*40)+(y*160),x
            }
        }

        inx
        cpx #40
        bne copy_line
    }

    ; poke char multicolor byte in Color Ram for all characters
    +clear_screen({ screen: $d800, clearByte: charcolors+3, usePtr: TRUE})

    rts
}

init_system: {

    sei
    lda #$35        			                    ; Bank out kernal and basic
    sta $01         			                    ; $e000-$ffff

    ; init music
	lda #0
	tax			
	tay			
    jsr sid.init	; jump to sid player init

    lda #0
    sta zp.xsintab_counter
    sta zp.ysintab_counter

    ; set all msb to 0 in case of
    !for i in range(SPRITE_COLS) {
        sta zp.x_positions+(i*2)+1
    }    

    ; set initial positions, first sinus position
    ldx xsintab
    !for i in range(SPRITE_COLS) {
        stx zp.x_positions+(i*2)
        +inc16(zp.x_positions+(i*2), 24*i)
    }

    ; set up initial y postions
    !for i in range(desc.nb_rows) {
        ; !let next_rasterline = TOP_POS+20+(i*20)-3  ; Set next rasterline to the next 20 pixels blocks - 3 to set the stable irq
        lda #TOP_POS+20+(i*20)-3
        sta zp.irq_lines+i
    }    

    +setup_irq(sprite_irqs, FIRST_IRQ)
    cli							                    ; set up irq to replace the kernal IRQ   

    rts
}

!!utils.debug(["end of INIT code", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $4300: Charset IRQ and routines
; ------------------------------------------------------------------------------------------
* = $4300

time_to_update:

    ldx frame_counter                           ; we update the charset only every UPDATE_EVERY frame
    cpx #UPDATE_EVERY                           ; else this is too fast (or would need more banks)
    beq do_update
    inx
    stx frame_counter
    rts

do_update: {
    +cpu_time_set_color(color.green)
    ldx #0
    stx frame_counter                           ; reset frame counter

    clc
    lda current_ptr                             ; patching the MSB is enough as each bank is stored at 256 boundary ($7000, $7100...)
    adc #>charsets                              ; means $70, $71, $72... dependening of the bank to read

!for i in range(8) {
    sta update_charset::start+(i*6)+2           ; self-modify the MSB, 8 times as we write 8 bytes
}
    ldx current_ptr                             ; set the charset ptr to the next charset
    inx            
    cpx #desc_charset.steps                     ; or reset if last bank

    bne no_reset
    ldx #0
no_reset:
    stx current_ptr
    +cpu_time_set_color(color.blue)
    jsr update_charset
    +cpu_time_set_color(color.black)

    rts
}

; routine to update the charset in $2000 
update_charset: {
    ldy #CHARSET_SIZE/8                         ; loop n times, n = charset size / 8
start:                                          ; as we write per batch of 8 bytes
!for i in range(8) { 
    lda $ee00+(i*CHARSET_SIZE/8),y              ; copy 8 bytes in n loops
    sta current_charset+(i*CHARSET_SIZE/8),y    ; msb ($ee) is self-modified by caller
}
    dey
    bpl start
    rts
}
!!utils.debug(["end of CHARSET IRQ code", *, maths.int_to_hex(*)])


; ------------------------------------------------------------------------------------------
; Segment $4400: Tables
; ------------------------------------------------------------------------------------------
* = $4400

; current charset displayed
current_ptr: !byte 0

; slow down the effet
frame_counter: !byte 0

; character memory template for tilemap
screencodes:
!for y in range(25) {
    !for x in range(40) {
        ; !!utils.debug((y*CHARSET_WIDTH)+(x % CHARSET_WIDTH))
        !byte (y*CHARSET_WIDTH)+(x % CHARSET_WIDTH)
    }
}

; charset colors
charcolors: !binary desc_charset.colorfile

!let xsinvals = maths.sintab(SINX_LEN, SINX_SCALE)
xsintab:
!for v in xsinvals {
    ; !!utils.debug([Math.round(SINX_PAD+v)])
    !byte Math.round(SINX_PAD+v)
}
end_xsintab:

!let ysinvals = maths.sintab(SINY_LEN, SINY_SCALE)
ysintab:
!for v in ysinvals {
    ; !!utils.debug(["y sin", Math.round(SINY_PAD+v)])
    !byte Math.round(60+v)
}
end_ysintab:

!!utils.debug(["end of TABLES", *, maths.int_to_hex(*)])

; ------------------------------------------------------------------------------------------
; Segment $7000: charsets banks
; ------------------------------------------------------------------------------------------
* = $7000
charsets: !binary desc_charset.datafile